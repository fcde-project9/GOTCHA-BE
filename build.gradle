plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.gotcha'
version = '0.0.1-SNAPSHOT'
description = 'Gacha shop service platform'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
		resolutionStrategy {
			// Pushy가 사용하는 Netty 취약점 수정 (CVE 대응)
			force 'io.netty:netty-handler:4.1.124.Final'
			force 'io.netty:netty-codec-http2:4.1.124.Final'
			force 'io.netty:netty-codec-http:4.1.124.Final'
			force 'io.netty:netty-transport:4.1.124.Final'
			force 'io.netty:netty-buffer:4.1.124.Final'
			force 'io.netty:netty-common:4.1.124.Final'
			force 'io.netty:netty-resolver:4.1.124.Final'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	implementation 'software.amazon.awssdk:s3:2.41.4'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'
	implementation 'com.github.ben-manes.caffeine:caffeine'
	implementation 'com.bucket4j:bucket4j_jdk17-core:8.16.1'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql:42.7.7'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	// Loki logging
	implementation 'com.github.loki4j:loki-logback-appender:2.0.3'
	// Logback conditional (for <if> in logback-spring.xml)
	implementation 'org.codehaus.janino:janino'
	// Web Push
	implementation 'nl.martijndwars:web-push:5.1.1'
	implementation 'org.bouncycastle:bcprov-jdk18on:1.83'
	// APNS (Native Push)
	implementation 'com.eatthepath:pushy:0.15.4'
	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
}

tasks.named('test') {
	useJUnitPlatform()
}
